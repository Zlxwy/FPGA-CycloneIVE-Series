//shift suquency(from the first to the last)
//DIG6   DIG5   DIG4   DIG3   DIG2   DIG1   DP     G      F      E      D      C      B      A
//sel[0] sel[1] sel[2] sel[3] sel[4] sel[5] seg[7] seg[6] seg[5] seg[4] seg[3] seg[2] seg[1] seg[0]
module hc595_ctrl
#(
  parameter cnt_MAX = 2'd3,
  parameter cnt_bit_MAX = 4'd13
)
(
  input   wire      sys_clk,
  input   wire      sys_rst_n,
  input   wire[5:0] sel,//位选信号，0~5代表低位到高位，即右到左（DIG6~DIG1）
  input   wire[7:0] seg,//段选信号
  
  output  reg       ds,
  output  reg       shcp,
  output  reg       stcp.
  output  wire      oe
);

wire[13:0]  data;//the 14-bit data to shift
reg[1:0]    cnt;//count by 'sys_clk', cycle counting between 0~3
reg[3:0]    cnt_bit;//count by 'cnt' update, cycle counting between 0~13

assign data = {seg[0],seg[1],seg[2],seg[3],seg[4],seg[5],seg[6],seg[7],sel[5],sel[4],sel[3],sel[2],sel[1],sel[0]};


/*'cnt' count between 0~3 circularly by 'sys_clk'*/
always @(posedge sys_clk or negedge sys_rst_n)
  if(sys_rst_n == 1'b0)
    cnt <= 2'd0;
  else if(cnt == cnt_MAX)
    cnt <= 2'd0;
  else
    cnt <= cnt + 1'b1;


/*'cnt_bit' count between 0~13 circularly by the update of 'cnt' counting*/
always @(posedge sys_clk or negedge sys_rst_n)
  if(sys_rst_n == 1'b0)
    cnt_bit <= 4'd0;
  else if(cnt_bit == cnt_bit_MAX && cnt == cnt_MAX)
    cnt_bit <= 4'd0;
  else if(cnt == cnt_MAX)
    cnt_bit <= cnt_bit + 4'd1;
  else
    cnt_bit <= cnt_bit;


/*output the data-bit at the 'ds' pin, depend on the value of 'cnt_bit', it's ever-changing.*/
always @(posedge sys_clk or negedge sys_rst_n)
  if(sys_rst_n == 1'b0)
    ds <= 1'b0;
  else if(cnt == 2'd0)
    ds <= data[cnt_bit];
  else
    ds <= ds;


/*bring the 'shcp' pin high, to make the databit be shifted into the shift register*/
always @(posedge sys_clk or negedge sys_rst_n)
  if(sys_rst_n == 1'b0)
    shcp <= 1'b0;
  else if(cnt == 2'd2)
    shcp <= 1'b1;
  else if(cnt == 2'd0)
    shcp <= 1'b0;
  else
    shcp <= shcp;


/*bring the 'stcp' pin high, to make the shifted data be transferred from the shift register to the output latches*/
always @(posedge sys_clk or negedge sys_rst_n)
  if(sys_rst_n == 1'b0)
    stcp <= 1'b0;
  else if(cnt_bit == 4'd0 && cnt == 2'd0)
    stcp <= 1'b1;
  else if(cnt_bit == 4'd0 && cnt == 2'd2)
    stcp <= 1'b0;
  else
    stcp <= stcp;


/*active low pin, bring it low all the time*/
assign oe = 1'b0;


endmodule